#!/usr/bin/perl 
#
# program: checks tc4400 modem status
# author, (c): Philippe Kueck <projects at unixadm dot org>
#
# requires: LWP::UserAgent HTML::TableExtract
#

use strict;
use warnings;

use utf8;
use LWP::UserAgent;
use HTML::TableExtract;
use Getopt::Long;
use Pod::Usage;

sub nagexit {
    my $exitc = {0 => 'OK', 1 => 'WARNING', 2 => 'CRITICAL', 3 => 'UNKNOWN'};
    printf "%s - %s|%s\n", $exitc->{$_[0]}, $_[1], $_[2]||"";
    exit $_[0]
}

my $config;
Getopt::Long::Configure("no_ignore_case");

GetOptions(
    'H=s' => \$config->{'host'},
    'u=s' => \$config->{'user'},
    'p=s' => \$config->{'pass'},
    'h|help' => sub {pod2usage({'-exitval' => 3, '-verbose' => 2})}
) or pod2usage({'-exitval' => 3, '-verbose' => 0});
pod2usage({'-exitval' => 3, '-verbose' => 0}) unless $config->{'host'};

my ($statusline, $perfdata, $retval) = ([], undef, 0);

sub check_downstream_snr {
    if ($_[1] eq "QAM256") {
        return 2 if $_[0] <= 30;
        return 1 if $_[0] <= 32
    } elsif ($_[1] eq "QAM64") {
        return 2 if $_[0] <= 24;
        return 1 if $_[0] <= 26
    }
    0
}

sub check_downstream_signal_level {
    return 2 if ($_[0] <= -8 || $_[0] >= 20.1);
    return 1 if ($_[0] <= -6 || $_[0] >= 18.1);
    0
}

sub check_upstream_signal_level {
    return 2 if ($_[0] <= 32 || $_[0] >= 54.1);
    return 1 if ($_[0] <= 34 || $_[0] >= 56.1);
    0
}

my $ua = new LWP::UserAgent;
$ua->agent("nagios/check_tc4400");
$ua->credentials(
    $config->{'host'}.":80", "Broadband Router",
    $config->{'user'}, $config->{'pass'}
);

local $SIG{ALRM} = sub {nagexit 3, "timeout"};
alarm 60;

my $resp = $ua->get(
    sprintf "http://%s/cmconnectionstatus.html", $config->{'host'}
);

nagexit 3, $resp->status_line unless $resp->is_success;

my $table = new HTML::TableExtract;
$table->parse($resp->decoded_content);

# startup procedure
my $ts = $table->table(0,0);
foreach my $r ($ts->rows) {
    if ($r->[0] =~ /^(?:Connectivity|Boot|Configuration)/ &&
        $r->[1] ne "OK") {
        $retval = 2;
        push @$statusline, sprintf "%s %s", $r->[0], $r->[1]
    }
    if ($r->[0] eq "Security" && $r->[1] ne "Enabled") {
        $retval = 2;
        push @$statusline, sprintf "%s %s", $r->[0], $r->[1]
    }
}

# Downstream Channel Status
$ts = $table->table(0,1);
foreach my $r ($ts->rows) {
    next unless $r->[0] =~ /^\d+$/;
    if ($r->[2] ne "Locked") {
        $retval = 1 if $retval < 1;
        push @$statusline, sprintf "dch%02d is %s", $r->[0], $r->[2];
        next
    }
    $perfdata->{'dn'}->{$r->[0]} = {
        'snr' => ($r->[7] =~ /([\d.]+) dB/)[0],
        'rlvl' => ($r->[8] =~ /([\d.]+) dBmV/)[0],
        'cwpass' => $r->[10],
        'cwcorr' => $r->[11],
        'cwfail' => $r->[12]
    };
    my $_retval = check_downstream_snr(
        $perfdata->{'dn'}->{$r->[0]}->{'snr'}, $r->[9]
    );
    if ($_retval > 0) {
        $retval = $_retval if $retval < $_retval;
        push @$statusline, sprintf
            "dch%02d snr (%.1fdB)", $r->[0],
                $perfdata->{'dn'}->{$r->[0]}->{'snr'};
    }
    $_retval = check_downstream_signal_level(
        $perfdata->{'dn'}->{$r->[0]}->{'rlvl'}
    );
    if ($_retval > 0) {
        $retval = $_retval if $retval < $_retval;
        push @$statusline, sprintf
            "dch%02d sig (%.1fdBmV)", $r->[0],
                $perfdata->{'dn'}->{$r->[0]}->{'rlvl'};
    }
}

# Upstream Channel Status
$ts = $table->table(0,2);
foreach my $r ($ts->rows) {
    next unless $r->[0] =~ /^\d+$/;
    if ($r->[2] ne "Locked") {
        $retval = 1 if $retval < 1;
        push @$statusline, sprintf "uch%02d is %s", $r->[0], $r->[2];
        next
    }
    $perfdata->{'up'}->{$r->[0]} = {
        'tlvl' => ($r->[7] =~ /([\d.]+) dBmV/)[0]
    };
    my $_retval = check_upstream_signal_level(
        $perfdata->{'up'}->{$r->[0]}->{'tlvl'}
    );
    if ($_retval > 0) {
        $retval = $_retval if $retval < $_retval;
        push @$statusline, sprintf
            "uch%02d sig (%.1fdBmV)", $r->[0],
                $perfdata->{'up'}->{$r->[0]}->{'tlvl'};
    }
}

nagexit $retval,
    @$statusline?join(" ", @$statusline):"OK",
    join(" ", map {
        my $s = $_;
        map {
            my $t = $_;
            map {
                sprintf "%s%02d_%s=".($_ =~ /^cw/?"%dc":"%.1f").";;;",
		$s, $t, $_,
                $perfdata->{$s}->{$t}->{$_}
            } sort keys %{$perfdata->{$s}->{$_}}
        } sort keys %{$perfdata->{$s}}
    } ('dn', 'up'))

__END__
=head1 NAME

check_tc4400

=head1 VERSION

0.1

=head1 SYNOPSIS

 check_tc4400 -H HOST -u USER -p PASS

=head1 OPTIONS

=over 8

=item B<H> I<HOSTNAME>

Hostname or ip address of your tc4400 modem.

=item B<u> I<username>

Username to authenticate with to the modem's webinterface.

=item B<p> I<password>

Password to authenticate with to the modem's webinterface.

=back

=head1 DESCRIPTION

This script connects to the TC4400 webinterface and parses the connection status page.

It warns or returns a critical state if:

 * Connectivity State is not "OK"
 * Boot State is not "OK"
 * Configuration File is not "OK"
 * Security is not "Enabled"
 * downstream channel is not "Locked"
 * downstream channel signal/noise ratio is below 32dB/30dB (QAM256)
 * downstream channel signal/noise ratio is below 24dB/26dB (QAM64)
 * downstream channel receive level is below -8dBmV/-6dBmV or above 18.1dBmV/20.1dBmV
 * upstream channel transmission level is below 32dBmV/34dBmV or above 54.1dBmV/56.1dBmV

=head1 DEPENDENCIES

=over 8

=item C<LWP::UserAgent>

=item C<HTML::TableExtract>

=item C<Pod::Usage>

=item C<Getopt::Long>

=back

=head1 AUTHOR

Philippe Kueck <projects at unixadm dot org>

=cut

